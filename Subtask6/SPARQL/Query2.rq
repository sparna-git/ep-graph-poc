# Query 2
# Topics related to open procedures at a given point in time
# Note : we don't have procedures end date, so only a start date criteria is used
PREFIX : <http://data.europa.eu/eli/ontology#>
PREFIX eli-dl: <http://data.europa.eu/eli/eli-draft-legislation-ontology#>
PREFIX elidl-ep: <http://data.europarl.europa.eu/ontology/elidl-ep#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?procedureReference ?eurovocTopic ?eurovocLabel
WHERE {
    # read a procedure with its reference
    ?procedure a eli-dl:LegislativeProcess .
    ?procedure eli-dl:legislative_process_id ?procedureReference .

    # navigate down to the reading I
    ?procedure eli-dl:consists_of ?reading_I .
    ?reading_I elidl-ep:activityType <http://data.europarl.europa.eu/authority/activity-type/READING_I> .    
    # then down to the plenary session
    ?reading_I eli-dl:consists_of ?plenarySession .
    ?plenarySession elidl-ep:activityType <http://data.europarl.europa.eu/authority/activity-type/PRVPL_I> .    
    # then navigate to the plenary dossier
    ?plenarySession eli-dl:based_on_a_realization_of ?plenaryDossier .    
    # and read eurovoc topics of plenary dossier
    ?plenaryDossier elidl-ep:legislativeProcessWorkIsAboutEurovoc ?eurovocTopic .
    # and read the label of the eurovoc topic if we have it
    OPTIONAL { ?eurovocTopic skos:prefLabel ?eurovocLabel . FILTER(langMatches(lang(?eurovocLabel), "en")) }
    
    # we are only interested in procedure that were created before a certain date
    ?procedure elidl-ep:creationDate ?dateCreation .
    FILTER(?dateCreation < $lookupDate)
    VALUES $lookupDate { "2019-05-14T00:00:00"^^xsd:dateTime }
    
    # theoretically we should also have a criteria on an end date of the procedure, that we don't have currently
    # so we don't know if the procedures we select were active or ended at that date
}
ORDER BY ?procedureReference
