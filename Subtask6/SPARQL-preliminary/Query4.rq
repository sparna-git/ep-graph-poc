# Query 4
# How many open main dossiers are processed by committee ?
PREFIX elidl: <http://data.europa.eu/eli/eli-draft-legislation-ontology#>
PREFIX elidl-ep: <http://data.europarl.europa.eu/ontology/elidl-ep#>
PREFIX eli-dl: <http://data.europa.eu/eli/eli-draft-legislation-ontology#>
PREFIX ep-aut: <http://data.europarl.europa.eu/authority/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
SELECT DISTINCT ?committee ?committeeName (count(?mainDossier) AS ?nbMainDossier)
WHERE {
    # the navigate that main dossier
    ?procedure eli-dl:consists_of ?reading .
    ?reading eli-dl:consists_of ?mainDossier .
    # filter the main dossier type
    ?mainDossier elidl-ep:activityType ?mainDossierType .
    VALUES ?mainDossierType {
        <http://data.europarl.europa.eu/authority/activity-type/MAIN_DOSSIER>
        <http://data.europarl.europa.eu/authority/activity-type/MAIN_MHE_DOSSIER>
    }
    # filter all activity participation type "committeeResponsible"
    ?mainDossier elidl-ep:hasActivityParticipation ?activityparticipation .
    ?activityparticipation elidl-ep:activityParticipationRole <http://data.europarl.europa.eu/authority/activity-participation-role/committeeResponsible> .
    ?activityparticipation elidl-ep:activityParticipationHasAgent ?committee .
    # select the name of the committee
    ?committee skos:prefLabel ?committeeName . FILTER(langMatches(lang(?committeeName), "en"))
}
GROUP BY ?committee ?committeeName
ORDER BY DESC(?nbMainDossier)